plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'io.github.dexman545.outlet' version '1.6.1'
	id 'com.modrinth.minotaur' version '2.+'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "compact-mob-farms"
version = "${project.mod_version}+${project.minecraft_version}"
outlet.mcVersionRange = project.supported_minecraft_versions


loom {
	splitEnvironmentSourceSets()

	mods {
		modid {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	accessWidenerPath = file("src/main/resources/compact-mob-farms.accesswidener")

	runs {
		datagenClient {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=compact-mob-farms"

			runDir "build/datagen"
		}
	}
}

sourceSets {
	main {
		resources {
			srcDirs += [
			        "src/main/generated"
			]
		}
	}
}

configurations {
	includeModImplementation

	include.extendsFrom           includeModImplementation
	modImplementation.extendsFrom includeModImplementation


	includeImplementation

	include.extendsFrom        includeImplementation
	implementation.extendsFrom includeImplementation
}

repositories {
	mavenCentral()
	maven {
		name = "JitPack"
		url  = "https://jitpack.io"
		content {
			includeGroup "com.github.llamalad7.mixinextras"
			includeGroup "top.offsetmonkey538"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${outlet.yarnVersion(project.minecraft_version)}:v2"
	modImplementation "net.fabricmc:fabric-loader:${outlet.loaderVersion()}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${outlet.fapiVersion(project.minecraft_version)}"

	// Uncomment for including a module of fabric api
	// includeModImplementation fabricApi.module("fabric-api-base", outlet.fapiVersion(project.minecraft_version))

	// Mixin Extras
	includeImplementation annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixinextras_version}")

	// Config library
	includeModImplementation "top.offsetmonkey538:monkeyconfig538:${project.monkeyconfig538_version}"
}

processResources {
	inputs.properties(Map.of(
			"modVersion",                 project.version,
			"supportedMinecraftVersions", project.supported_minecraft_versions
	))

	filesMatching("fabric.mod.json") {
		expand(Map.of(
				"modVersion",                 project.version,
				"supportedMinecraftVersions", project.supported_minecraft_versions
		))
	}

	exclude ".cache/**"
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "compact-mob-farms"
	def customVersionName = System.getenv("VERSION_NAME")
	if (customVersionName != null) versionName = customVersionName
	versionNumber = "${project.version}"
	versionType = "beta"
	uploadFile = remapJar.archiveFile
	//additionalFiles = [sourcesJar.archiveFile, javadocJar.archiveFile]
	additionalFiles = [sourcesJar.archiveFile]
	gameVersions = outlet.mcVersions()
	syncBodyFrom = rootProject.file("README.md").text
	def changelogFile = rootProject.file("CHANGELOG.md")
	if (changelogFile.exists()) {
		changelog = changelogFile.text
	}

	dependencies {
		required.project "fabric-api"
	}
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
